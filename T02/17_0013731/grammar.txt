program → declarationList

declarationList → declarationList declaration | declaration

declaration → variableDeclaration | functionDeclaration

variableDeclaration → TYPE ID ;

functionDeclaration → TYPE ID (params) compoundStmt

params → paramList | e

paramList → paramList, param | param

param → TYPE ID

compoundStmt → { statementList }

statementList → statementList statement | e

statement → expressionStmt | compoundStmt |conditionalStmt | loopStmt | returnStmt | variableDeclaration | inOutStmt

expressionStmt → expression; | ;

conditionalStmt → if (expression) statement | if (expression) statement else statement 

loopStmt → for (expression ; logicExpression ; expression) statement

returnStmt → return expression;

inOutStmt → INPUT ( ID ) ; | OUTPUT ( outputArgs ) ;

expression → ID = expression | logicExpression

logicExpression → logicExpression OP_LOGIC relatExpression | relatExpression

relatExpression → relatExpression OP_RELAT listExpression | listExpression

listExpression →  addExpression OP_LIST listExpression | addExpression

addExpression → addExpression OP_ADD mulExpression | mulExpression

mulExpression → mulExpression OP_MUL factor | factor

factor → (expression) | unaryExpression | call | ID | FLOAT | INT | NIL

unaryExpression → UN_OP factor | OP_ADD factor

call → ID (args)

outputArgs →  factor | STRING

args → argList | e

argList → argList, expression | expression